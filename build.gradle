import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    dependencies {
        classpath 'com.pascalwelsch.gitversioner:gitversioner:0.4.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

sourceCompatibility = 1.11

String mainClassName = 'dev.markusk.bluelight.miner.Launcher'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://nexus.markusk.dev/repository/maven-public/'
    }
}

dependencies {
    implementation 'dev.markusk:fetcher-api:+'

    // https://mvnrepository.com/artifact/io.prometheus/simpleclient_httpserver
    implementation group: 'io.prometheus', name: 'simpleclient_httpserver', version: '0.8.1'
    // https://mvnrepository.com/artifact/org.yaml/snakeyaml
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'

    /*Logging and console*/
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    implementation 'org.jline:jline:3.10.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
    /*--Logging and console--*/

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// https://github.com/passsy/gradle-gitVersioner-plugin
apply plugin: 'com.pascalwelsch.gitversioner'
gitVersioner {
    def base = "$System.env.BASE"
    def snap = "$System.env.SNAP"
    baseBranch base == null ? 'development' : base
    def snapshot = snap == null ? true : snap != 'false'
    addSnapshot snapshot

    addLocalChangesDetails false
    if (snapshot) {
        formatter = { gitVersioner ->
            return "${gitVersioner.currentSha1Short}-SNAPSHOT"
        }
    }
}

group 'dev.markusk'
version gitVersioner.versionName

processResources {
    filter(ReplaceTokens, tokens: [VERSION: version])
}

task sourcesForRelease(type: Copy) {
    from 'src/main/java'
    into 'build/filteredSrc'
    filter(ReplaceTokens, tokens: [VERSION: version, DATE: getBuildDate(), TIME: getBuildTime()])
}

static def getBuildDate() {
    return new Date().format('yyyy/MM/dd')
}

static def getBuildTime() {
    return new Date().format('HH:mm:ss')
}

compileJava {
    options.encoding = 'UTF-8'
    source = sourcesForRelease.destinationDir
    classpath = sourceSets.main.compileClasspath
    dependsOn sourcesForRelease
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from "${buildDir}/filteredSrc"
}

jar {
    archivesBaseName = project.name
    manifest {
        attributes('Implementation-Version': archiveVersion, 'Main-Class': mainClassName)
    }
}

shadowJar {
    archiveClassifier.set('withDependencies')
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter sourcesJar
}

task getGitVersion {
    println gitVersioner.versionName
}